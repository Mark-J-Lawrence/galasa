plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
}

group   = "dev.galasa"

// Note: The following line is changed by the set-version.sh script.
// It is also read by other build scrips as required.
version = "0.32.0"

signing {
    sign publishing.publications
}

tasks.withType(Sign) {
    onlyIf { isMainOrRelease.toBoolean() }
}

allprojects {
   tasks.withType(Javadoc) {
      options.addStringOption('Xdoclint:none', '-quiet')
   }

    repositories {
        mavenLocal()
        maven {
            url "https://development.galasa.dev/main/maven-repo/obr/"
        }
        gradlePluginPortal()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'jacoco'
    test {
        finalizedBy jacocoTestReport
    }
    jacocoTestReport {
        dependsOn test
    }
}

// Publish the release.yaml as a maven artifact.
// Note: The maven co-ordinates are versioned using the version for this bundle.

publishing {
    afterEvaluate {
        publications {
            pluginMaven(MavenPublication) {
            }

            withType(MavenPublication) {
                pom {
                    url = 'https://galasa.dev'
                    name = 'Galasa Gradle Plugins parent'
                    description = 'This project contains the Galasa Gradle plugins and common plugin code'
                    licenses {
                        license {
                            name = 'Eclipse Public License - v 2.0'
                            url = 'https://www.eclipse.org/legal/epl-2.0'
                        }
                    }
                    developers {
                        developer {
                            name = 'Michael Baylis'
                            email = 'Michael.Baylis@uk.ibm.com'
                            organization = 'IBM'
                            organizationUrl = 'https://www.ibm.com'
                        }
                        developer {
                            name = 'William Yates'
                            email = 'wyates@uk.ibm.com'
                            organization = 'IBM'
                            organizationUrl = 'https://www.ibm.com'
                        }
                        developer {
                            name = 'Michael Cobbett'
                            email = 'mike_cobbett@uk.ibm.com'
                            organization = 'IBM'
                            organizationUrl = 'https://www.ibm.com'
                        }
                        developer {
                            name = 'Jade Carino'
                            email = 'jade.carino@ibm.com'
                            organization = 'IBM'
                            organizationUrl = 'https://www.ibm.com'
                        }
                        developer {
                            name = 'Savvas Kyriacou'
                            email = 'savvasky@uk.ibm.com'
                            organization = 'IBM'
                            organizationUrl = 'https://www.ibm.com'
                        }
                        developer {
                            name = 'Aaron Jhaj'
                            email = 'aaron.jhaj@ibm.com'
                            organization = 'IBM'
                            organizationUrl = 'https://www.ibm.com'
                        }
                        developer {
                            name = 'Eamonn Mansour'
                            email = 'eamonn.mansour@ibm.com'
                            organization = 'IBM'
                            organizationUrl = 'https://www.ibm.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git:://github.com/galasa-dev/gradle'
                        developerConnection = 'scm:git:git:://github.com/galasa-dev/gradle'
                        url = 'https://github.com/galasa-dev/gradle'
                    }
                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/galasa-dev/projectmanagement/issues'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url  = "$targetMaven"
            
            if ("$targetMaven".startsWith('http')) {
                credentials {
                    username System.getenv('MAVENUSERNAME')
                    password System.getenv('MAVENPASSWORD')
                }
            }
        }
    }

}




